import id.syafii.ataskcalculator.libs.AndroidX
import id.syafii.ataskcalculator.libs.Google
import id.syafii.ataskcalculator.libs.JetBrains

plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'com.google.dagger.hilt.android'
}

android {
    namespace 'id.syafii.ataskcalculator'
    compileSdk 33

    defaultConfig {
        applicationId "id.syafii.ataskcalculator"
        minSdk 26
        targetSdk 33
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
    buildFeatures {
        dataBinding = true
        viewBinding = true

        //add this if using build config on flavor
        buildConfig = true
    }


    flavorDimensions 'feature'
    productFlavors {

        redFilesystem {
            dimension "feature"
            applicationIdSuffix ".red.filesystem"
            resValue "string", "app_name", "App Red Filesystem"
            manifestPlaceholders = [appTheme: "@style/AppTheme.Red"]
            buildConfigField "boolean", "isFilesystemEnabled", "true"
        }
        redBuiltInCamera {
            dimension "feature"
            applicationIdSuffix ".red.built_in_camera"
            resValue "string", "app_name", "App Red Built-in Camera"
            manifestPlaceholders = [appTheme: "@style/AppTheme.Red"]
            buildConfigField "boolean", "isFilesystemEnabled", "false"

        }

        greenFilesystem {
            dimension "feature"
            applicationIdSuffix ".green.filesystem"
            resValue "string", "app_name", "App Green Filesystem"
            manifestPlaceholders = [appTheme: "@style/AppTheme.Green"]
            buildConfigField "boolean", "isFilesystemEnabled", "true"
        }

        greenBuiltInCamera {
            dimension "feature"
            applicationIdSuffix ".green.built_in_camera"
            resValue "string", "app_name", "App Green Built-in Camera"
            manifestPlaceholders = [appTheme: "@style/AppTheme.Green"]
            buildConfigField "boolean", "isFilesystemEnabled", "false"

        }
    }

    applicationVariants.all { variant ->
        def flavorName = variant.productFlavors.find { it.dimension == 'feature' }?.name ?: ""

        // Replace multiple underscores with a single hyphen
        def formattedFeature = flavorName.replaceAll(/_+/,'-')

        // Insert hyphens between camel case words and make it lowercase
        formattedFeature = formattedFeature.replaceAll(/([a-z])([A-Z])/,'$1-$2').toLowerCase()

        // Set a custom output file name based on the formatted flavor name
        variant.outputs.all { output ->
            outputFileName = "app-${formattedFeature}.apk"
        }
    }
}

dependencies {

    implementation Google.material
    implementation Google.gson
    implementation Google.hiltAndroid
    kapt Google.hiltAndroidCompiler
    implementation Google.mlkitTextTecognition
    implementation AndroidX.constraintLayout
    testImplementation AndroidX.junit
    androidTestImplementation AndroidX.testJunit
    androidTestImplementation AndroidX.testEspresso
    implementation AndroidX.activityKtx
    implementation AndroidX.fragmentKtx
    implementation AndroidX.lifecycleLivedata
    implementation AndroidX.lifecycleViewModel
    implementation AndroidX.lifecycleRuntime
    implementation AndroidX.swipeRefresh
    implementation AndroidX.coreKtx
    implementation AndroidX.appCompact
    implementation JetBrains.coroutinesPlayServices
    implementation JetBrains.coroutinesAndroid
}